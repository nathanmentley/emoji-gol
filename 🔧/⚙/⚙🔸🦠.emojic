💭🔜
    Game Of Life - Conway's Game Of Life written in emojicode

    Copyright 2022 Nathan Mentley
    
    Permission is hereby granted, free of charge, to any person obtaining a copy of this software and associated documentation files (the "Software"), to deal in the Software without restriction, including without limitation the rights to use, copy, modify, merge, publish, distribute, sublicense, and/or sell copies of the Software, and to permit persons to whom the Software is furnished to do so, subject to the following conditions:
    
    The above copyright notice and this permission notice shall be included in all copies or substantial portions of the Software.
    
    THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
🔚💭

💭 The play command that'll execute a game of life game.
🐇 ⚙🔸🦠 🍇
  🐊 🔶🐍⚙
  
  🆕 🍇🍉

  💭 Return a list of parameters
  🔓 ❓ 🖇 ➡️  🍨🐚🔶🐍📎🍆🍇
    💭 Right now, we don't accept any parameters, but in the future it'd be nice to be able to configure the game.
    ↩️ 🆕🍨🐚🔶🐍📎🍆❗️
  🍉

  💭 Return a command name
  🔓 ❓ 📛 ➡️ 🔡🍇
    ↩️ 🔤Play🔤
  🍉

  💭 Return a command description
  🔓 ❓ 💬 ➡️ 🔡🍇
    ↩️ 🔤Runs the Game Of Life Simulation🔤
  🍉

  💭 Define some logic for the command to run when it's executed
  🔓 ❗️ 🏃 args 🍯🐚🔡🍆 ➡️ 🔢🍇
    💭 Generates a random game of life map that's 80 by 80 cells
    🆕 🎲🔸🗺️ 120 120 ❗️ ➡️ gameOfLifeMap

    💭 for each tick while the game is still running.
    🔁 ⏱️ gameOfLifeMap ❗ 🍇
      💭 render

      🔂 x 🆕⏩ 0 📏 gameOfLifeMap ❓ ❗️ 🍇
        🆕🔠 📏 gameOfLifeMap ❓ ❗️ ➡️ stringBuilder

        🔂 y 🆕⏩ 0 📐 gameOfLifeMap ❓ ❗️ 🍇
          ↪️ 💙 gameOfLifeMap x y ❓ 🍇
            🐻 stringBuilder 🔤█🔤 ❗️
          🍉
          🙅 🍇
            🐻 stringBuilder 🔤 🔤 ❗️
          🍉
        🍉

        😀 🔡 stringBuilder ❗️ ❗️️
      🍉

      💭 sleep
      ⏲ 🐇 🧵 100000 ❗️
    🍉

    ↩️ 0
  🍉
🍉