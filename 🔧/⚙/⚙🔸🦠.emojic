💭🔜
    Game Of Life - Conway's Game Of Life written in emojicode

    Copyright 2022 Nathan Mentley
    
    Permission is hereby granted, free of charge, to any person obtaining a copy of this software and associated documentation files (the "Software"), to deal in the Software without restriction, including without limitation the rights to use, copy, modify, merge, publish, distribute, sublicense, and/or sell copies of the Software, and to permit persons to whom the Software is furnished to do so, subject to the following conditions:
    
    The above copyright notice and this permission notice shall be included in all copies or substantial portions of the Software.
    
    THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
🔚💭

📗A class that contains the clihelper command logic to run the GameOfLife.📗
🐇 ⚙🔸🦠 🍇
  💭 This class implements the 🔶🐍⚙ interface defined by clihelper, to give this a nice cli interface.
  💭  We essentially need to just define details about a specific command that this app can perform.
  💭  In this case, it's to run the Game Of Life simluation.
  🐊 🔶🐍⚙
  
  📗Constructor📗
  🆕 🍇🍉

  📗Return a list of parameters📗
  💭 
  🔓 ❓ 🖇 ➡️  🍨🐚🔶🐍📎🍆🍇
    🆕🍨🐚🔶🐍📎🍆❗️ ➡️  🖍 🆕 ret
    
    🐻ret
      🆕🔶🐍📎 
        🔤Width🔤 💭command name
        🔤The width of the map🔤 💭command description
        🔤width🔤 💭command key. Used to as program arg 1 to call the command
        🔤w🔤 💭optional short key.
        🔤80🔤 💭optional default value
        👎 💭is this a flag parameter
        👎 💭is this parameter requred
      ❗️
    ❗️

    🐻ret
      🆕🔶🐍📎 
        🔤Height🔤 💭command name
        🔤The height of the map🔤 💭command description
        🔤height🔤 💭command key. Used to as program arg 1 to call the command
        🔤h🔤 💭optional short key.
        🔤80🔤 💭optional default value
        👎 💭is this a flag parameter
        👎 💭is this parameter requred
      ❗️
    ❗️

    🐻ret
      🆕🔶🐍📎 
        🔤Delay🔤 💭command name
        🔤The delay in microseconds between each tick of the simulation🔤 💭command description
        🔤delay🔤 💭command key. Used to as program arg 1 to call the command
        🔤d🔤 💭optional short key.
        🔤50000🔤 💭optional default value
        👎 💭is this a flag parameter
        👎 💭is this parameter requred
      ❗️
    ❗️

    ↩️ ret
  🍉

  📗Return a command name📗
  🔓 ❓ 📛 ➡️ 🔡🍇
    ↩️ 🔤Play🔤
  🍉

  📗Return a command description📗
  🔓 ❓ 💬 ➡️ 🔡🍇
    ↩️ 🔤Runs the Game Of Life Simulation🔤
  🍉

  📗Runs the logic to run the GOL simulation, and print it to the screen📗
  🔓 ❗️ 🏃 args 🍯🐚🔡🍆 ➡️ 🔢🍇
    📏 👇 args ❓ ➡ width
    📐 👇 args ❓ ➡ height
    ⏱ 👇 args ❓ ➡ delay

    💭 Generates a random game of life map using the 🎲🔸🗺️ class.
    🆕 🎲🔸🗺️ width height ❗️ ➡️ gameOfLifeMap

    💭 for each tick while the game is still running.
    🔁 ⏱️ gameOfLifeMap ❗ 🍇
      💭 render the current game state to the screen
      🔂 x 🆕⏩ 0 📏 gameOfLifeMap ❓ ❗️ 🍇
        🆕🔠 📏 gameOfLifeMap ❓ ❗️ ➡️ rowContentStringBuilder

        🔂 y 🆕⏩ 0 📐 gameOfLifeMap ❓ ❗️ 🍇
          ↪️ 💙 gameOfLifeMap x y ❓ 🍇
            🐻 rowContentStringBuilder 🔤█🔤 ❗️
          🍉
          🙅 🍇
            🐻 rowContentStringBuilder 🔤 🔤 ❗️
          🍉
        🍉

        😀 🔡 rowContentStringBuilder ❗️ ❗️️
      🍉

      💭 sleep
      ⏲ 🐇 🧵 delay ❗️
    🍉

    ↩️ 0
  🍉

  📗gets the width of the game📗
  🔒 ❓ 📏 args 🍯🐚🔡🍆 ➡️ 🔢 🍇
    ↪️  🐽 args 🔤width🔤 ❗️ ➡️ width 🍇
      ↪️ 🔢 width 10 ❗ ➡️ width  🍇
        ↩️ width
      🍉
    🍉

    ↩️ 80
  🍉

  📗gets the height of the game📗
  🔒 ❓ 📐 args 🍯🐚🔡🍆 ➡️ 🔢 🍇
    ↪️  🐽 args 🔤height🔤 ❗️ ➡️ height 🍇
      ↪️ 🔢 height 10 ❗ ➡️ height  🍇
        ↩️ height
      🍉
    🍉

    ↩️ 80
  🍉

  📗gets the height of the game📗
  🔒 ❓ ⏱️ args 🍯🐚🔡🍆 ➡️ 🔢 🍇
    ↪️  🐽 args 🔤delay🔤 ❗️ ➡️ delay 🍇
      ↪️ 🔢 delay 10 ❗ ➡️ delay  🍇
        ↩️ delay
      🍉
    🍉

    ↩️ 50000
  🍉
🍉