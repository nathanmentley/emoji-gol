💭🔜
    Game Of Life - Conway's Game Of Life written in emojicode

    Copyright 2022 Nathan Mentley
    
    Permission is hereby granted, free of charge, to any person obtaining a copy of this software and associated documentation files (the "Software"), to deal in the Software without restriction, including without limitation the rights to use, copy, modify, merge, publish, distribute, sublicense, and/or sell copies of the Software, and to permit persons to whom the Software is furnished to do so, subject to the following conditions:
    
    The above copyright notice and this permission notice shall be included in all copies or substantial portions of the Software.
    
    THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
🔚💭

🐇 🎲🔸🗺️ 🍇
  🖍🆕 mapWidth 🔢
  🖍🆕 mapHeight 🔢
  🖍🆕 map 🍨🐚️🍨🐚️🎲🔸🦠🍆🍆

  💭 CTOR
  🆕 width 🔢 height 🔢 🍇
    💭 set instance data
    width ➡️ 🖍mapWidth
    height ➡️ 🖍mapHeight
    🆕 🍨🐚️🍨🐚️🎲🔸🦠🍆🍆❗️ ➡️ 🖍map

    💭 populate map
    🆕🎰❗ ➡ randomNumberGen

    🔂 x 🆕⏩ 0 mapWidth❗️ 🍇
      🆕 🍨🐚️🎲🔸🦠🍆❗ ➡ 🖍🆕 column

      🔂 y 🆕⏩ 0 mapHeight❗️ 🍇
        🔢 randomNumberGen 1 2 ❗️ ➡️ isCellAlive
        🆕 🎲🔸🦠 isCellAlive 🙌 1 ❗ ➡️ cell

        🐻 column cell ❗
      🍉

      🐻 map column ❗
    🍉
  🍉

  💭 gets the width of the game
  🔓 ❓ 📏 ➡️ 🔢 🍇
    ↩️ mapWidth
  🍉

  💭 gets the height of the game
  🔓 ❓ 📐 ➡️ 🔢 🍇
    ↩️ mapHeight
  🍉

  💭 gets if a specific cell is alive
  🔓 ❓ 💙 x 🔢 y 🔢 ➡️ 👌 🍇
    ↪️ x ▶️🙌 mapWidth 🍇
      ↩️ 👎
    🍉
    ↪️ y ▶️🙌 mapHeight 🍇
      ↩️ 👎
    🍉
    ↪️ x ◀️ 0 🍇
      ↩️ 👎
    🍉
    ↪️ y ◀️ 0 🍇
      ↩️ 👎
    🍉

    🦠 👇 x y ❓ ➡ cell

    ↩️ 💙 cell ❓
  🍉

  💭 runs a single tick of the game
  🔓 ❗️ ⏱️ ➡️ 👌 🍇
    🆕 🍨🐚️🎲🔸🦠🍆❗ ➡ 🖍🆕 cellsToKill
    🆕 🍨🐚️🎲🔸🦠🍆❗ ➡ 🖍🆕 cellsToRevieve

    🔂 x 🆕⏩ 0 mapWidth❗️ 🍇
      🔂 y 🆕⏩ 0 mapHeight❗️ 🍇
        ↪️ 💙 👇 x y ❓ 🤝 💀 👇 x y ❓ 🍇
          🦠 👇 x y ❓ ➡ cell

          🐻 cellsToKill cell ❗️
        🍉

        ↪️ 💙 👇 x y ❓ 🙌 👎 🤝 🏥 👇 x y ❓ 🍇
          🦠 👇 x y ❓ ➡ cell

          🐻 cellsToRevieve cell ❗️
        🍉
      🍉
    🍉

    🔂 cell cellsToKill 🍇
      💙 cell 👎 ❗️
    🍉

    🔂 cell cellsToRevieve 🍇
      💙 cell 👍 ❗️
    🍉

    ↩️ 📏 cellsToKill ❓ ▶️ 0 👐 📏 cellsToRevieve ❓ ▶️ 0 
  🍉

  🔒 ❓ 💀 x 🔢 y 🔢 ➡️ 👌 🍇
    ↩️
      🧮 👇 x y ❓ 🙌 1 👐
      🧮 👇 x y ❓ 🙌 4 👐
      🧮 👇 x y ❓ 🙌 5 👐
      🧮 👇 x y ❓ 🙌 6 👐
      🧮 👇 x y ❓ 🙌 7 👐
      🧮 👇 x y ❓ 🙌 8
  🍉

  🔒 ❓ 🏥 x 🔢 y 🔢 ➡️ 👌 🍇
    ↩️ 🧮 👇 x y ❓ 🙌 3
  🍉

  🔒 ❓ 🧮 x 🔢 y 🔢 ➡️ 🔢 🍇
    0 ➡️ 🖍🆕 count

    ↪️ 💙 👇 x ➖ 1 y ❓ 🍇
      count ⬅️ ➕ 1
    🍉
    ↪️ 💙 👇 x ➕ 1 y ❓ 🍇
      count ⬅️ ➕ 1
    🍉
    ↪️ 💙 👇 x y ➖ 1 ❓ 🍇
      count ⬅️ ➕ 1
    🍉
    ↪️ 💙 👇 x y ➕ 1 ❓ 🍇
      count ⬅️ ➕ 1
    🍉
    ↪️ 💙 👇 x ➖ 1 y ➖ 1 ❓ 🍇
      count ⬅️ ➕ 1
    🍉
    ↪️ 💙 👇 x ➕ 1 y ➖ 1 ❓ 🍇
      count ⬅️ ➕ 1
    🍉
    ↪️ 💙 👇 x ➕ 1 y ➕ 1 ❓ 🍇
      count ⬅️ ➕ 1
    🍉
    ↪️ 💙 👇 x ➖ 1 y ➕ 1 ❓ 🍇
      count ⬅️ ➕ 1
    🍉

    ↩️ count
  🍉

  🔒 ❓ 🦠 x 🔢 y 🔢 ➡️ 🎲🔸🦠 🍇
    ↩️ 🐽 🐽 map x ❗️ y ❗️
  🍉
🍉